// Copyright (C) 2002-2011 Hazardous Software Inc.  See EULA for license information.

#include common/include/unit_common_header.aih
int uclass = unit->Class;

if(uclass==$DOME_CLASS){
    //Domes can only atk every 18 ticks and only while they have full ammo
    if (unit->Ammo < 100 || (unit->Age % 18) != 0) { 
      PERFORM NOTHING; 
    }
}
else {
   PERFORM RAND;
    if (perf_ret > 8192) { //relax  3/4 of the time, go easy on the cpu
      PERFORM NOTHING; 
    } 
}

int qmaxHP = 0; //cache query.MaxHP
//find weakest enemy unit...
PERFORM GET_MAX_ACTION_RANGE_RADIUS $ACTION_ATTACK;
query_vis_distance = perf_ret;
target = QUERY UNIT [unit] MIN [ query<_>unit * query->HP] 
                       WHERE [!(query->IsAlly) && query.Rank>=$TURRET_RANK && (qmaxHP=query.MaxHP)>0 && query->IsStasis==0 ];  //don't look for buildings except turrets (rank>=90)
if (target->IsAlive!=1) {                   
    //look for any building
    target = QUERY UNIT [unit] MIN [  query->HP ] WHERE [!(query->IsAlly) && (qmaxHP=query.MaxHP)>0 && query->IsStasis==0 ]; 
    if (target->IsAlive!=1) {   
        PERFORM NOTHING;
    }

}   

//select proper weapon
int targetOnGround = target->IsTerrainGround;
int weaponAction = $ACTION_ATTACK_W1;
if (uclass==$DOME_CLASS || uclass == $DOME_BEAM_CLASS) {			
    if (targetOnGround) { weaponAction = $ACTION_ATTACK_POS; }
    else { weaponAction = $ACTION_ATTACK; }	
} else {
    if (targetOnGround) weaponAction = $ACTION_ATTACK;	
}	

PERFORM GET_CLOSEST_TARGET_POSITION_WITHIN_RANGE weaponAction;
int position = perf_ret;
if (position!=$INVALID_POSITION) { 
    object enemy = target;
    target = position;
    
    PERFORM IS_POSITION_WITHIN_ACTION_ANGLE weaponAction;
    if (perf_ret>0) { //target is attackable in this direction
        
        //dome beam the target if it has more than 220 hp and unit has more than 50 (enough time to fire)
        if (uclass==$DOME_BEAM_CLASS && unit->Ammo==100 && enemy->HP >= 120 && unit->HP >= 50) {	
            target = enemy;
            PERFORM SET_PARAM target;
            PERFORM SET_OBJECTIVE $OBJ_BLDG_LAUNCH;
            PERFORM NOTHING;
        }

        target_is_position = 1;
        // Fire weapons
        if (weaponAction == $ACTION_ATTACK_POS) {
            PERFORM $ACTION_ATTACK_POS position;
        } else {
            if (weaponAction == $ACTION_ATTACK) {
                PERFORM $ACTION_ATTACK position;
            } else {
                PERFORM $ACTION_ATTACK_W1 position;
            }
        }			
        PERFORM NOTHING;
    }
    else {
        PERFORM $ACTION_ROTATE enemy->BestAttackDirection;
        PERFORM NOTHING;
    }
}

